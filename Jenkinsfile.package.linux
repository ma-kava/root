#!groovy

def pixetBuildsDir = "/media/nox2/Software/Builds/pixet/releases"
def customersDir = "/media/nox2/Software/Public/pixet/customers/"

pipeline {
  agent {
    label 'linux01' 
  }

  options {
    timeout(time: 15, unit: 'MINUTES')
    timestamps()
    ansiColor("xterm")
  }

  parameters {
    string(name: 'customerName', 
      defaultValue: '', 
      description: 'Name of the customer.\nInternal licence with no explicit configuration files will be used if empty.\nValue also determine the folder the configuration and factory configuration will be copied from and the result will be placed to.\nPlease place configuration files to \"nox2/advacam/Software/pixet/customers/CustomerName/_in\" folder, the built packages will be created in \"nox2/advacam/Software/pixet/customers/CustomerName\" folder.')  
    booleanParam(name: 'api', 
      defaultValue: true,
      description: "Add also API to the package.")
    booleanParam(name: 'edu', 
      defaultValue: false,
      description: "Create EDU version of the package.")
    choice(name: 'license',
      choices: ['external', 'internal'],
      description: "Choose license.")
   }

  environment {
    TERM = 'xterm'
  }

  stages {

    stage('Copying files') {
      steps{
        script{
          
          stage('Copy PIXet') {
            def lastPixetBuild = sh (script: """ls -d ${pixetBuildsDir}/*/ | sort | tail -1""", returnStdout: true).trim()
            sh """
              mkdir -p _build/devices_configs
              cp ${lastPixetBuild}/*Linux.zip _build
              """
            env.PIXET_VERSION = sh( script: """ls _build/PIXet_Pro*Linux.zip | grep -o \'[0-9]*\\.[0-9]*\\.[0-9]*\'""", returnStdout: true).trim()

            if ( params.customerName.trim().length() ){
              def sourceDir = customersDir + "/" + params.customerName.trim().replaceAll(" ","\\\\ ") + "/_in"
              sh """
                if [ "\$(ls -A ${sourceDir})"  ]; then
                  cp ${sourceDir}/* _build/devices_configs
                fi
                """          
            }
          }
        }
      }
    }
  
  
    stage('Creating packages') {
      steps{
        script{
          def workspacePath = env.WORKSPACE
          def packagerParams = "deb rpm targz"
          if (params.edu) {
            packagerParams = packagerParams + " edu"
          }
          if (params.api) {
            packagerParams = packagerParams + " api"
          }
          if (params.customerName.trim().length()) {
            packagerParams = packagerParams + " -lic \\\"" + params.customerName.trim() + "\\\"";
          }
          sh """
             docker run --rm --user 1001:1001 -v "${workspacePath}":/opt/PixetPackage:Z -t ubuntu22.04dev_64 bash -c "cd /opt/PixetPackage; bash ./package_linux.sh -pversion ${env.PIXET_VERSION} ${packagerParams}"
             """    
        }
      }
    }

    stage('Deploying to nox2') {
      steps{
        script{
          def destinationDir = sh (script: """ls -d ${pixetBuildsDir}/*/ | sort | tail -1""", returnStdout: true).trim()          
          if ( params.customerName.trim().length() ){
            destinationDir = customersDir + "/" + params.customerName.trim().replaceAll(" ","\\\\ ")
          }
          def VER = params.edu ? "EDU" : "Pro"
          sh """
            mkdir -p ${destinationDir} 
            rm -f ${destinationDir}/PIXet_${VER}_${env.PIXET_VERSION}_Linux.deb ${destinationDir}/PIXet_${VER}_${env.PIXET_VERSION}_Linux.rpm ${destinationDir}/PIXet_${VER}_${env.PIXET_VERSION}_Linux.tar.gz
            cp _build/pixet.deb ${destinationDir}/PIXet_${VER}_${env.PIXET_VERSION}_Linux.deb
            cp _build/pixet.rpm ${destinationDir}/PIXet_${VER}_${env.PIXET_VERSION}_Linux.rpm
            cp _build/pixet.tar.gz ${destinationDir}/PIXet_${VER}_${env.PIXET_VERSION}_Linux.tar.gz
            """
          if (params.api) {
            sh """
              rm -f ${destinationDir}/API_PIXet_${VER}_${env.PIXET_VERSION}_Linux.tar.gz
              cp _build/pixetAPI.tar.gz ${destinationDir}/API_PIXet_${VER}_${env.PIXET_VERSION}_Linux.tar.gz
              """            
          }
        }
      }
    }

  }

  post {
    unsuccessful {
      slackSend channel:  "#jenkins", message: "PIXet packaging for linux failed.\n <${currentBuild.absoluteUrl}|${currentBuild.absoluteUrl}>"
    }
    always {
      cleanWs()
    }
  }    

}
